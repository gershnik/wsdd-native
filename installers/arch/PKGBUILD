# Maintainer: Eugene Gershnik <gershnik@hotmail.com>
pkgname='wsdd-native'
pkgver='1.21'
pkgrel=2
pkgdesc='WS-Discovery Host Daemon. Makes your machine visible in Network view of Windows Explorer'
arch=('x86_64' 'aarch64')
url='https://github.com/gershnik/wsdd-native'
license=('BSD-3-Clause')
depends=('glibc' 'gcc-libs')
makedepends=('cmake>=3.25' 'make' 'gcc>=11.3' 'git' 'patch' 'tar' 'libsystemd')
conflicts=('wsdd')
backup=('etc/wsddn.conf')
source=(
    "wsddn-v$pkgver.tgz::https://github.com/gershnik/wsdd-native/tarball/v$pkgver"
    "argum-v2.6.tgz::https://github.com/gershnik/argum/tarball/v2.6"
    "asio-1.30.2.tgz::https://downloads.sourceforge.net/asio/asio-1.30.2.tar.gz"
    "fmt-11.2.0.tgz::https://github.com/fmtlib/fmt/tarball/11.2.0"
    "isptr-v1.9.tgz::https://github.com/gershnik/intrusive_shared_ptr/tarball/v1.9"
    "libxml2-v2.14.5.tgz::https://gitlab.gnome.org/GNOME/libxml2/-/archive/v2.14.5/libxml2-v2.14.5.tar.gz"
    "modern-uuid-v1.8.tgz::https://github.com/gershnik/modern-uuid/tarball/v1.8"
    "outcome-v2.2.12.tgz::https://github.com/ned14/outcome/tarball/v2.2.12"
    "ptl-v1.7.tgz::https://github.com/gershnik/ptl/tarball/v1.7"
    "spdlog-v1.15.3.tgz::https://github.com/gabime/spdlog/tarball/v1.15.3"
    "sys_string-v2.20.tgz::https://github.com/gershnik/sys_string/tarball/v2.20"
    "tomlplusplus-v3.4.0.tgz::https://github.com/marzer/tomlplusplus/tarball/v3.4.0"
)
noextract=("${source[@]%%::*}")

_fetched_deps=( )

prepare() {
    local u
    for u in ${source[@]}; do
        local tgz="$SRCDEST/${u%%::*}"
        local comp=$(basename ${tgz%-*})
        [ -d "$comp" ] && rm -rf "$comp"
        mkdir "$comp"
        tar -C "$comp" --strip-components=1 --warning=no-unknown-keyword -xzf $tgz
        if [ $comp != "wsddn" ]; then
            _fetched_deps+=( "$comp" )
        fi
    done
}

build() {
    local fetch_sources=( '-DFETCHCONTENT_FULLY_DISCONNECTED=ON' )
    local comp
    for comp in "${_fetched_deps[@]}"; do
        fetch_sources+=( "-DFETCHCONTENT_SOURCE_DIR_${comp^^}=$srcdir/$comp" )
    done

    cd wsddn
    cmake -S . -B out -DCMAKE_BUILD_TYPE=None "${fetch_sources[@]}"
    cmake --build out
    cp installers/wsddn.conf out/
    sed -i "s/{RELOAD_INSTRUCTIONS}/# sudo systemctl restart wsddn\n/g" out/wsddn.conf
    sed -i "s/{SAMPLE_IFACE_NAME}/eth0/g" out/wsddn.conf
}

package() {
    cd wsddn
    cmake --install out --prefix $pkgdir/usr 
    install -Dm 0644 config/systemd/usr/lib/systemd/system/wsddn.service \
                            $pkgdir/usr/lib/systemd/system/wsddn.service
    install -Dm 0644 config/firewalls/etc/ufw/applications.d/wsddn \
                            $pkgdir/etc/ufw/applications.d/wsddn
    install -Dm 0644 out/wsddn.conf $pkgdir/etc/wsddn.conf
    install -Dm 0644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

}

